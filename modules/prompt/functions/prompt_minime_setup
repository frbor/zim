#
# minime theme
# Inspired by gitster
# https://github.com/shashankmehta/dotfiles/blob/master/thesetup/zsh/.oh-my-zsh/custom/themes/gitster.zsh-theme
#

gst_get_pwd() {
  prompt_short_dir="$(short_pwd)"
  git_root="$(command git rev-parse --show-toplevel 2> /dev/null)" && \
  prompt_short_dir="${prompt_short_dir#${$(short_pwd $git_root):h}/}"
  print ${prompt_short_dir}
}

virtualenv_prompt_info() {
  if [[ -n ${VIRTUAL_ENV} ]]; then
    print "%F{cyan}$(basename ${VIRTUAL_ENV}) "
  fi
}

prompt_context() {
  if [[ -n ${SSH_CONNECTION} ]]; then
    print "%F{cyan}%(!.%{%F{blue}%}.)%m "
  fi
}

prompt_gitster_precmd() {
  PROMPT='$(prompt_context)%F{green}$(gst_get_pwd) $(git_prompt_info)$(virtualenv_prompt_info)%f'
}

prompt_gitster_setup() {
  ZSH_THEME_GIT_PROMPT_PREFIX="%F{yellow}"
  ZSH_THEME_GIT_PROMPT_SUFFIX="%f"
  ZSH_THEME_GIT_PROMPT_DIRTY=" %F{red}✗%f "
  ZSH_THEME_GIT_PROMPT_CLEAN=" %F{green}✓%f "

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_gitster_precmd
  prompt_opts=(cr subst percent)
}

prompt_gitster_setup "$@"
